plugins {
  id("java")
  id("application") // this project includes multiple programs, not just one.

  // To create a fat jar build/libs/...-all.jar, run: ./gradlew shadowJar
  id("com.gradleup.shadow").version("9.3.2")

  // Code formatting; defines targets "spotlessApply" and "spotlessCheck"
  // Version 8.0.0 and later requires Java 17+.
  id("com.diffplug.spotless").version("8.2.1")

  // Error Prone linter
  id("net.ltgt.errorprone").version("5.0.0") apply false

  // PMD linter
  id("pmd")

  // code coverage
  id("jacoco")

  // Checker Framework pluggable type-checking
  id("org.checkerframework").version("1.0.2")

  // GraalVM native compilation
  id("org.graalvm.buildtools.native").version("0.11.4")
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url = "https://central.sonatype.com/repository/maven-snapshots/" }
  gradlePluginPortal()
}

ext {
  errorproneVersion = "2.47.0"
  isJava17orHigher = JavaVersion.current() >= JavaVersion.VERSION_17
  isJava21orHigher = JavaVersion.current() >= JavaVersion.VERSION_21
}

dependencies {
  implementation("com.google.googlejavaformat:google-java-format:1.33.0")
  implementation("org.plumelib:javac-parse:0.9.0")
  implementation("org.plumelib:plume-util:1.13.0")

  annotationProcessor("info.picocli:picocli-codegen:4.7.7")
  implementation("info.picocli:picocli:4.7.7")

  testImplementation("org.junit.jupiter:junit-jupiter:6.0.3")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

// Don't use a toolchain because it uses `--release`, which is incompatible with `--add-exports`.
java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED"
  //   // For Picocli native image
  //   options.compilerArgs += [
  //     "-Aproject=${project.group}/${project.name}"
  //   ]
}
compileTestJava {
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"
  options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED"
}
test {
  doFirst {
    jvmArgs += [
      "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
      "--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
      "--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
      "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
      "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
      "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
    ]
  }

  testLogging {
    // TODO: What about "ignored"?  There is no such event: https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/testing/logging/TestLogEvent.html
    events("passed", "skipped", "failed", "standardOut", "standardError")
  }

  testLogging {
    outputs.upToDateWhen{false}
    showStandardStreams = true
    events("failed")
    exceptionFormat = "full"
  }
}

tasks.named("test") {
  // Use JUnit Platform for unit tests.
  useJUnitPlatform()
}

tasks.register("runMakefileTests", Exec) {
  description = "Run tests defined in Makefile."
  group = "Verification"
  commandLine("make", "-C", file("src/test/resources").absolutePath)
}
check.dependsOn(runMakefileTests)
// The "test" task runs the JUnit tests, whose failure is more informative than system tests.
runMakefileTests.mustRunAfter(test)

jacocoTestReport {
  reports {
    xml.required = false
    csv.required = true // Output is written to build/reports/jacoco/test/jacocoTestReport.csv
    html.required = true // Output is written to build/reports/jacoco/test/html/index.html
  }
}
// Two possible configurations:
// Configuration A: report is always generated after tests run.
test.finalizedBy(jacocoTestReport)
// Configuration B: user must run `jacocoTestReport` explicitly.
// jacocoTestReport.dependsOn(test)

spotless {
  format "misc", {
    // define the files to apply `misc` to
    target("*.md", ".gitignore")

    // define the steps to apply to those files
    trimTrailingWhitespace()
    leadingTabsToSpaces(2)
    endWithNewline()
  }
  java {
    targetExclude("**/diff_match_patch.java")
    googleJavaFormat()
    formatAnnotations()
  }
  groovyGradle {
    target("**/*.gradle")
    greclipse()  // which formatter Spotless should use to format .gradle files.
    leadingTabsToSpaces(2)
    trimTrailingWhitespace()
    // endWithNewline() // Don't want to end empty files with a newline
  }
}

application {
  mainClass = "org.plumelib.merging.Main"
}

graalvmNative {
  binaries {
    main {
      imageName = "plumelib-merge"
      mainClass = "org.plumelib.merging.Main"
      buildArgs.add("-O4")
    }
    test {
      buildArgs.add("-O0")
    }
  }
  binaries.all {
    buildArgs.add("--verbose")
    // These arguments are from google-java-format's core/pom.xml.
    buildArgs.add("-H:+UnlockExperimentalVMOptions")
    buildArgs.add("-H:IncludeResourceBundles=com.sun.tools.javac.resources.compiler")
    buildArgs.add("-H:IncludeResourceBundles=com.sun.tools.javac.resources.javac")
    buildArgs.add("--no-fallback")
    buildArgs.add("--initialize-at-build-time=com.sun.tools.javac.file.Locations")
    buildArgs.add("-H:+ReportExceptionStackTraces")
    buildArgs.add("-H:-UseContainerSupport")
    buildArgs.add("-J--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED")
    buildArgs.add("-J--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED")
    buildArgs.add("-J--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED")
    buildArgs.add("-J--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED")
    buildArgs.add("-J--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED")
    buildArgs.add("-J--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED")
    buildArgs.add("-march=compatibility")
  }
}

// Error Prone linter

tasks.withType(JavaCompile).configureEach {
  options.forkOptions.jvmArgs += "-Xmx6g"
  options.compilerArgs << "-Werror"
  // "-processing" avoids javac warning "No processor claimed any of these annotations".
  if (isJava21orHigher) {
    options.compilerArgs << "-Xlint:all,-processing,-this-escape"
  } else {
    options.compilerArgs << "-Xlint:all,-processing"
  }
}
if (isJava21orHigher) {
  apply plugin: "net.ltgt.errorprone"
  dependencies {
    errorprone("com.google.errorprone:error_prone_core:${errorproneVersion}")
  }
  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      disable("AnnotateFormatMethod") // Error Prone doesn't know about Checker Framework @FormatMethod.
      disable("DoNotCallSuggester") // Suggests use of an Error Prone annotation.
      disable("EffectivelyPrivate") // Loses information about the abstraction.
      disable("ExtendsObject")  // Incorrect when using the Checker Framework.
      disable("InlineMeSuggester") // Using `@InlineMe` requires clients to declare a dependency on error_prone_annotations.
      disable("PatternMatchingInstanceof") // requires Java 16 or later
      disable("ReferenceEquality") // Use Interning Checker instead.
      disable("StatementSwitchToExpressionSwitch") // requires Java 12 or later
      disable("StringConcatToTextBlock") // requires Java 15 or later
      // Don't complain about external library (which is included as source because it isn't at Maven Central).
      excludedPaths = ".*/diff_match_patch.java"
    }
  }
}


// PMD linter

pmd {
  toolVersion = "7.21.0"
  ruleSetFiles = files("${rootDir}/.pmd-ruleset.xml")
  consoleOutput = true
  ignoreFailures = false
  incrementalAnalysis = true
}
pmdTest.enabled = false


// Checker Framework pluggable type-checking

apply plugin: "org.checkerframework"

checkerFramework {
  version = libs.checker.get().version
  checkers = [
    // No need to run CalledMethodsChecker, because ResourceLeakChecker does so.
    // "org.checkerframework.checker.calledmethods.CalledMethodsChecker",
    "org.checkerframework.checker.formatter.FormatterChecker",
    // Passing the Index Checker should be possible; reinstate this when I have time to do so.
    // "org.checkerframework.checker.index.IndexChecker",
    "org.checkerframework.checker.interning.InterningChecker",
    "org.checkerframework.checker.lock.LockChecker",
    "org.checkerframework.checker.nullness.NullnessChecker",
    "org.checkerframework.checker.regex.RegexChecker",
    "org.checkerframework.checker.resourceleak.ResourceLeakChecker",
    "org.checkerframework.checker.signature.SignatureChecker",
    "org.checkerframework.checker.signedness.SignednessChecker",
    "org.checkerframework.common.initializedfields.InitializedFieldsChecker",
  ]
  extraJavacArgs = [
    "-Werror",
    // "-Aversion",
    // "-verbose",
    "-AcheckPurityAnnotations",
    "-ArequirePrefixInWarningSuppressions",
    "-AwarnRedundantAnnotations",
    "-AwarnUnneededSuppressions",
    "-AskipDefs=.*\\.diff_match_patch",
  ]
}


// Javadoc

tasks.withType(Javadoc) {
  options.noTimestamp = true
}
javadoc {
  // Turn Javadoc warnings into errors.
  options.addStringOption("Xwerror", "-Xdoclint:all")
  options.addStringOption("private", "-quiet")
  options.addStringOption("source", "17")
  // Buggy per https://github.com/java9-modularity/gradle-modules-plugin/issues/170
  // moduleOptions {
  //   addExports = [
  //     "jdk.compiler/com.sun.tools.javac.parser" : "ALL-UNNAMED",
  //     "jdk.compiler/com.sun.tools.javac.tree" : " ALL-UNNAMED" ,
  //     "jdk.compiler/com.sun.tools.javac.util':"ALL-UNNAMED"
  //   ]
  // }
  // Workaround until bug is fixed.
  options {
    addMultilineStringsOption("-add-exports").setValue([
      "jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
      "jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
      "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
      "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED"
    ])
  }
  doLast {
    ant.replaceregexp(match:"@import url\\('resources/fonts/dejavu.css'\\);\\s*", replace:"",
    flags:"g", byline:true) {
      fileset(dir: destinationDir)
    }
  }
}
check.dependsOn(javadoc)

tasks.register("javadocWeb", DefaultTask) {
  description = "Upload API documentation to website."
  dependsOn(javadocWebUpload, javadocWebChgrp, javadocWebChmod)
}
tasks.register("javadocWebUpload", Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/${project.name}/api")
  classpath = project.sourceSets.main.compileClasspath
  options.addStringOption("source", "11")
  doLast {
    ant.replaceregexp(match:"@import url\\('resources/fonts/dejavu.css'\\);\\s*", replace:"",
    flags:"g", byline:true) {
      fileset(dir: destinationDir)
    }
  }
}
// Set permissions
tasks.register("javadocWebChgrp", Exec) {
  commandLine("chgrp", "-R",  "plse_www", "/cse/web/research/plumelib/${project.name}/api")
  ignoreExitValue = true
}
javadocWebChgrp.mustRunAfter("javadocWebUpload")
tasks.register("javadocWebChmod", Exec) {
  commandLine("chmod", "-R",  "g+w", "/cse/web/research/plumelib/${project.name}/api")
  ignoreExitValue = true
}
javadocWebChmod.mustRunAfter("javadocWebUpload")


configurations {
  requireJavadoc
}
dependencies {
  requireJavadoc("org.plumelib:require-javadoc:2.0.0")
}
def requireJavadoc = tasks.register("requireJavadoc", JavaExec) {
  group = "Documentation"
  description = "Ensures that Javadoc documentation exists."
  inputs.files(sourceSets.main.allJava)
  mainClass = "org.plumelib.javadoc.RequireJavadoc"
  classpath = configurations.requireJavadoc
  args(sourceSets.main.allJava.srcDirs.collect{it.getAbsolutePath()})
  jvmArgs += [
    "--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
    "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
    "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
    "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
    "--add-opens=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
    "--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
    "--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
    "--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
  ]
}
check.dependsOn(requireJavadoc)

// Emacs support

/* Make Emacs TAGS table */
tasks.register("tags", Exec) {
  description = "Run etags to create an Emacs TAGS table"
  commandLine("bash", "-c", "find src/ \\( -name '*.java' -o -name '*.sh' \\) | sort | xargs etags")
}
